/* a template for the reliable broadcast: Byzantine faults and clean crashes */

skel Proc {
  /* the control locations are as follows:
    0 is the initial state with value 0,
    1 is the initial state with value 1,
    2 is the sent state,
    3 is the accepting state,
    4 is the crashed state,
   */
  local pc;
  /* the number of messages sent by the correct processes */
  shared nsnt, ncrashed;
  parameters N, Tb, Tcc, Fb, Fcc;
  unknowns a1, b1, c1, d1, a2, b2, c2, d2; /* the constants to be found by synthesis */

  assumptions (0) {
    N > 3 * Tb + Tcc;
    Tb >= Fb;
    Tb >= 1;
    Tcc >= Fcc;
    Tcc >= 1;
  }


  locations (0) {
    loc0: [0];
    loc1: [1];
    loc2: [2];
    loc3: [3];
    loc4: [4];
  }

  inits (0) {
    (loc0 + loc1) == N - Fb;
    loc2 == 0;
    loc3 == 0;
    loc4 == 0;
    nsnt == 0;
    ncrashed == 0;
  }

  rules (0) {
  /* crashing without sending a message */
  0: loc0 -> loc4
      when (ncrashed < Fcc)
      do { nsnt' == nsnt; ncrashed' == ncrashed + 1; };
  1: loc1 -> loc4
      when (ncrashed < Fcc)
      do { nsnt' == nsnt; ncrashed' == ncrashed + 1; };
  /* crashing when in the sent or accepting state */
  2: loc2 -> loc4
      when (ncrashed < Fcc)
      do { nsnt' == nsnt; ncrashed' == ncrashed + 1; };
  3: loc3 -> loc4
      when (ncrashed < Fcc)
      do { nsnt' == nsnt; ncrashed' == ncrashed + 1; };
  /* send a message when having value 1 */
  4: loc1 -> loc2
      when (nsnt >= 0)
      do { nsnt' == nsnt + 1; ncrashed' == ncrashed; };
  /* accept directly */
  5: loc0 -> loc3
      when (nsnt >= (a2 * N + b2 * Tb + c2 * Tcc + d2 - Fb))
      do { nsnt' == nsnt + 1; ncrashed' == ncrashed; };
  6: loc1 -> loc3
      when (nsnt >= (a2 * N + b2 * Tb + c2 * Tcc + d2 - Fb))
      do { nsnt' == nsnt + 1; ncrashed' == ncrashed; };
  /* send a message */
  7: loc0 -> loc2
      when (nsnt >= (a1 * N + b1 * Tb + c1 * Tcc + d1 - Fb))
      do { nsnt' == nsnt + 1; ncrashed' == ncrashed; };
  /* accept */
  8: loc2 -> loc3
      when (nsnt >= (a2 * N + b2 * Tb + c2 * Tcc + d2 - Fb))
      do { nsnt' == nsnt; ncrashed' == ncrashed; };
  /* self-loops in all the locations but loc1, from which processes must progress */
  9: loc0 -> loc0
      when (nsnt >= 0)
      do { nsnt' == nsnt; ncrashed' == ncrashed; };
  10: loc2 -> loc2
      when (nsnt >= 0)
      do { nsnt' == nsnt; ncrashed' == ncrashed; };
  11: loc3 -> loc3
      when (nsnt >= 0)
      do { nsnt' == nsnt; ncrashed' == ncrashed; };
  }
  specifications (3) {
    /* safety */
    unforg: (loc1 == 0) -> [](loc3 == 0);
    corr: <>[]((nsnt < a1 * N + b1 * Tb + c1 * Tcc + d1 || loc0 == 0)
                && (nsnt < a2 * N + b2 * Tb + c2 * Tcc + d2 || loc0 == 0)
                && ((nsnt < a2 * N + b2 * Tb + c2 * Tcc + d2) || loc2 == 0)
                && (loc1 == 0))
        -> ((loc0 == 0) -> <>(loc3 != 0));
    relay: <>[]((nsnt < a1 * N + b1 * Tb + c1 * Tcc + d1 || loc0 == 0)
                && (nsnt < a2 * N + b2 * Tb + c2 * Tcc + d2 || loc0 == 0)
                && ((nsnt < a2 * N + b2 * Tb + c2 * Tcc + d2) || loc2 == 0)
                && (loc1 == 0))
        -> []((loc3 != 0) -> <>((loc0 == 0) && (loc1 == 0) && (loc2 == 0)));
  }
} /* Proc */


